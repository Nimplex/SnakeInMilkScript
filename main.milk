#import "std"
#import "array"
#import "math"
#import "love"

game = []
reset = () {
  game = { 
    draw: (x, y) -> love.graphics.rectangle("fill", (x-1) * 20, (y-1) * 20, 19, 19)
    screen: { w: 64, h: 36 }
    food: { x: 25, y: 32 }
    directions: []
    segments: []
    alive: true
    score: 0
    timer: 0
  }
  array.add(game.segments, { x: game.screen.w / 2, y: game.screen.h / 2, direction: "a" })
  array.add(game.segments, { x: (game.screen.w / 2)+1, y: game.screen.h / 2, direction: "a" })
  array.add(game.segments, { x: (game.screen.w / 2)+2, y: game.screen.h / 2, direction: "a" })
}


love.load = () {
  reset()
  love.window.setMode(game.screen.w * 20, game.screen.h * 20, { resizable: false, vsync: true })
  love.window.setTitle("Snake in MilkScript, made by Nimplex.")
  love.window.setIcon(love.image.newImageData("assets/icon.png"))
  math.randomize_seed(std.time())
}

love.update = (dt) {
  if game.alive {
    food_spawn = []
    game.timer += dt
    if game.timer >= 0.2 {
      game.timer -= 0.2
      for direction: game.directions {
        for segment: game.segments {
          if direction.x == segment.x && direction.y == segment.y segment.direction = direction.dir
        }
      }
      for direction: game.directions {
        if direction {
          for segment: game.segments {
            if direction.x == segment.x && direction.y == segment.y {
              if array.indexof(game.segments, segment) == std.len(game.segments)-1 {
                array.delete(game.directions, array.indexof(game.directions, direction))
              }
            }
          }
        }
      }
      for segment: game.segments {
        for segment2: game.segments {
          if array.indexof(game.segments, segment) != array.indexof(game.segments, segment2) && segment.x == segment2.x && segment.y == segment2.y game.alive = false
        }
      }
      if game.segments[0].x == game.food.x && game.segments[0].y == game.food.y {
        lastSegment = game.segments[std.len(game.segments)-1]
        if lastSegment.direction == "w" {
          array.add(game.segments, { x: lastSegment.x, y: lastSegment.y+1, direction: lastSegment.direction })
        } else if lastSegment.direction == "s" {
          array.add(game.segments, { x: lastSegment.x, y: lastSegment.y-1, direction: lastSegment.direction })
        } else if lastSegment.direction == "a" {
          array.add(game.segments, { x: lastSegment.x+1, y: lastSegment.y, direction: lastSegment.direction })
        } else if lastSegment.direction == "d" {
          array.add(game.segments, { x: lastSegment.x-1, y: lastSegment.y, direction: lastSegment.direction })
        }
        for i: 1..64 {
          for i2: 1..36 {
            possible = true
            for segment: game.segments {
              if segment.y == i2 && segment.x == i possible = false
            }
            if possible array.add(food_spawn, { x: i, y: i2 })
          }
        }
        game.score+=1
        fod = food_spawn[math.random(0, std.len(food_spawn)-1)]
        game.food.x = fod.x
        game.food.y = fod.y
      } 
      for segment: game.segments {
        switch segment.direction {
          case "w" {
            segment.y -= 1
          }
          case "s" {
            segment.y += 1
          }
          case "a" {
            segment.x -= 1
          }
          case "d" {
            segment.x += 1
          }
        }
      }
    }
    if game.segments[0].x == 0 || game.segments[0].x == game.screen.w || game.segments[0].y == 0 || game.segments[0].y == game.screen.h game.alive = false
  }
}

love.keypressed = (key) {
  if game.alive {
    if std.len(game.directions)-3 >= std.len(game.segments) return
      if key == "w" && game.segments[0].y-1 == game.segments[1].y || 
         key == "s" && game.segments[0].y+1 == game.segments[1].y || 
         key == "a" && game.segments[0].x-1 == game.segments[1].x || 
         key == "d" && game.segments[0].x+1 == game.segments[1].x return
      array.add(game.directions, { x: game.segments[0].x, y: game.segments[0].y, dir: key })
  } else if key == "space" {
    reset()
  }
}

RGB = (R, G, B) => R / 255, G / 255, B / 255

love.draw = () {
  if game.alive {
    love.graphics.setColor(RGB(255, 255, 0))
    game.draw(game.food.x, game.food.y)
    for segment: game.segments {
      love.graphics.setColor(RGB(255, 255, 255))
      game.draw(segment.x, segment.y)
    }
    love.graphics.print("Score: "..std.to_string(game.score), love.graphics.newFont(20), (1280/2)-20, 20)
  } else {
    love.graphics.setColor(RGB(255, 0, 0))
    love.graphics.print("Game over.", love.graphics.newFont(50), (1280/2)-140, (720/2)-50)
    love.graphics.setColor(RGB(255, 255, 255))
    love.graphics.print("Score: "..std.to_string(game.score), love.graphics.newFont(30), (1280/2)-50, 20)
  }
}
